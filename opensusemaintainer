#!/usr/bin/perl -w
use strict;
use CGI qw":form :cgi start_html end_html br"; # "head" would collide with LWP:Simple
use XML::Simple;
use LWP::Simple;
# needs libcrypt-ssleay-perl for HTTPS
use DB_File;
use Fcntl;


sub suseapi
{
    my $urlbase='https://api.opensuse.org/public';
    my $url=$urlbase."/".join("/", @_);
    my $xml=get($url);
    return unless $xml;
    return XMLin($xml, ForceArray => 1);
}

sub lookupdbm($$)
{
    my ($db,$key)=@_;
    my %dbmap;
    my $result;
    tie %dbmap, "DB_File", "/home/aw/html/db.suse/$db.dbm", O_RDONLY;
    $result=$dbmap{$key};
    untie %dbmap;
    return $result;
}

our %seen=();
sub printusers($)
{ my $data=shift;
    my $ps;
    unless ($data && ($ps=$data->{person})) {
        print "No persons associated with this package<br/>\n";
        return;
    } else {
        foreach my $p (@$ps) {
            my $user=$p->{userid};
            if($user eq "--separatordummy--") { print "<hr/>"; next;}
            next if $seen{$user.$p->{role}}++;
            print qq{$p->{role}: <a href="https://build.opensuse.org/user/show/$user">$user</a>\n<br/>};
        }
    }
}

sub selftest()
{
    lookupdbm("filepkg", "/bin/bash") eq "bash" or die "filepkg selftest failed";
    lookupdbm("pkgsrc", "Mesa-32bit") eq "Mesa" or die "pkgsrc selftest failed";
}

# main

print header("text/html"), start_html;
my $input1=$ENV{PATH_INFO}||""; $input1=~s{^/}{}; param("pkg", $input1) if $input1;
$input1=param("pkg");
#$input1=~s/[^a-zA-Z0-9._-]//g; # sanitize FIXME

my %typeselect=(
    "auto" => "autodetect",
    "srcpkg" => "source package name (e.g. Mesa)",
    "binpkg" => "binary package name (e.g. Mesa-32bit)",
    "fullpath" => "full path/filename (e.g. /bin/bash)",
    "command" => "command/executable (e.g. bash)");
if(!param) {
    print start_form(-name=>"form", -method=>"get"),
        popup_menu(-name=>'type',-default=>"auto", -values=>[sort keys %typeselect], -labels=>\%typeselect)," input type",br,
        textfield(-name=>'pkg', -class=>'text'),br,
        submit(),end_form,end_html;
}
my %output;
my $type=param('type') || "auto";
$type="fullpath" if $input1=~m{/};
if($type ne "auto") {
    $output{$type}=$input1;
} else {
    for my $t ("srcpkg", "binpkg", "command") {
        $output{$t}=$input1;
    }
}
if($output{"command"}) {
    foreach my $path (qw"/bin /sbin /usr/bin /usr/sbin") {
        my $p=lookupdbm("filepkg", $path."/".$output{"command"});
        $output{binpkg}=$p if $p;
    }
}
if($output{"fullpath"}) {
    $output{"fullpath"}=~s!/$!!; # drop trailing slash
    my $p=lookupdbm("filepkg", $output{"fullpath"});
    $output{binpkg}=$p if $p;
    print "file not found".br unless $p;
}
if($output{binpkg}) {
    my $p=lookupdbm("pkgsrc", $output{binpkg});
    $output{srcpkg}=$p if $p;
    print "binary package not found".br unless $p || $type eq "auto";
}
if(my $pkgname=$output{srcpkg}) {
    my $data=suseapi("source", "openSUSE:Factory", $pkgname, "_meta");
    if((my $devel=$data->{devel}->[0])) {
        print "Source-Package: $pkgname",br,
        qq!devel-project: <a href="https://build.opensuse.org/package/show/$devel->{project}/$devel->{package}">$devel->{project}</a><br/>Package-Maintainers:<br/>\n!;
        $data=suseapi("source", $devel->{project}, $devel->{package}, "_meta");
        printusers($data);
        print "<hr/> Project-Maintainers:".br;
        $data=suseapi("source", $devel->{project}, "_meta");
        printusers($data);
    } else { print "Bad package on Factory" }
}

selftest;
